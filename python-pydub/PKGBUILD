# Maintainer: Simon Legner <Simon.Legner@gmail.com>
pkgname=python-pydub
pkgver=0.25.0
pkgrel=1
pkgdesc="Manipulate audio with an simple and easy high level interface"
url="https://pydub.com"
license=('MIT')
arch=('any')
optdepends=('python-pyaudio: for playback' 'python-simpleaudio: for playback' 'ffmpeg: for playback' 'libav: for playback')
makedepends=('python-setuptools')
source=("https://pypi.org/packages/source/p/pydub/pydub-$pkgver.tar.gz"
        "https://github.com/jiaaro/pydub/raw/v$pkgver/LICENSE")
sha256sums=('c3fb5f3c60c4eb4af84666342507e8983f3a96b87b6ef90e13b996d4402942e2'
            'ae856536224cc7a389e968771fc5f259814bdd0da63539cf72282adbaef6897a')

build() {
  cd "$srcdir/pydub-$pkgver"
  python setup.py build
}

check() {
  cd "$srcdir/pydub-$pkgver"
  python setup.py check
}

package() {
  cd "$srcdir/pydub-$pkgver"
  python setup.py install --root="$pkgdir" --optimize=1 --skip-build

  install -Dm644 "$srcdir/LICENSE" "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}
