# Maintainer: Simon Legner <Simon.Legner@gmail.com>
pkgbase=python-pydub
pkgname=('python-pydub' 'python2-pydub')
pkgver=0.24.1
pkgrel=1
pkgdesc="Manipulate audio with an simple and easy high level interface"
url="http://pydub.com"
license=('MIT')
arch=('any')
source=("$pkgbase-$pkgver.tar.gz::https://github.com/jiaaro/pydub/archive/v$pkgver.tar.gz")
makedepends=('python-setuptools' 'python2-setuptools')

build() {
  cd "$srcdir"
  cp -r pydub-$pkgver pydub2-$pkgver
}

check() {
  cd "$srcdir/pydub-$pkgver"
  which python3 && python3 setup.py check

  cd "$srcdir/pydub2-$pkgver"
  which python2 && python2 setup.py check
}

package_python-pydub() {
  depends=('python' 'python-setuptools')
  cd "$srcdir/pydub-$pkgver"
  python3 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

package_python2-pydub() {
  depends=('python2' 'python2-setuptools')
  cd "$srcdir/pydub2-$pkgver"
  python2 setup.py install --prefix=/usr --root="$pkgdir" --optimize=1
  install -Dm644 LICENSE "$pkgdir/usr/share/licenses/$pkgname/LICENSE"
}

sha256sums=('92b13680b1cd5152c3b437063a7290f17e5c4ea0bcd8c64ae239950313d2d650')
